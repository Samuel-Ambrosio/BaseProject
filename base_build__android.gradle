import com.samuelav.buildsrc.AppConfig
import com.samuelav.buildsrc.dependencies.Dependencies
import com.samuelav.buildsrc.dependencies.Plugins
import com.samuelav.buildsrc.dependencies.Versions

apply plugin: Plugins.kotlinAndroid
apply from: "$rootDir/base_build__common.gradle"

android {
    compileSdkVersion AppConfig.compileSdkVersion
    buildToolsVersion AppConfig.buildToolsVersion
    flavorDimensions "default"

    defaultConfig {
        versionCode AppConfig.versionCode
        versionName AppConfig.versionName
        targetSdk AppConfig.targetSdkVersion
        minSdk AppConfig.minSdkVersion

        testInstrumentationRunner AppConfig.testInstrumentationRunner
        vectorDrawables {
            useSupportLibrary true
        }
    }

    productFlavors {
        dev {
            buildConfigField "String", "API_BASE_PATH", "\"https://jsonplaceholder.typicode.com/\""
        }
        pre {
            buildConfigField "String", "API_BASE_PATH", "\"https://jsonplaceholder.typicode.com/\""
        }
        pro {
            buildConfigField "String", "API_BASE_PATH", "\"https://jsonplaceholder.typicode.com/\""
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
        }
        release {
            minifyEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        buildConfig true
    }

   composeOptions {
        kotlinCompilerExtensionVersion Versions.AndroidX.composeCompiler
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }

    kotlinOptions {
        jvmTarget = AppConfig.kotlinJvmTarget
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    implementation Dependencies.AndroidX.coreKtx
    implementation Dependencies.AndroidX.appCompat

    implementation Dependencies.Google.material

    implementation platform(Dependencies.Google.firebaseBoM)
    implementation Dependencies.Google.firebaseCrashlytics

    // TODO: Remove '//' after google-services.json file and ad id are added
    //implementation Dependencies.Google.playServicesAds

    implementation platform(Dependencies.AndroidX.composeBoM)
    implementation Dependencies.AndroidX.composeRuntime
    coreLibraryDesugaring Dependencies.Android.desugarJdkLibs

    testImplementation Dependencies.Test.junit
    androidTestImplementation Dependencies.Test.androidJunit
    androidTestImplementation Dependencies.Test.androidEspresso
}